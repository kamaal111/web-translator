# List available commands
default:
    just --list --unsorted

# Run dev server
dev: prepare
    bun run dev

# Build assets
build output="dist": prepare
    BUILD_OUTPUT={{output}} bun run build

# Build and watch assets
build-watch output="dist": prepare
    BUILD_OUTPUT={{output}} bun run build:watch

# Prepare project for development
prepare: install-modules generate-client

# Install modules
install-modules:
    bun install

# Generate API client from OpenAPI spec
generate-client:
    bunx openapi-generator-cli generate \
        -i src/openapi.yaml \
        -g typescript-fetch \
        -o src/generated/api-client \
        --additional-properties=supportsES6=true,npmName=api-client,typescriptThreePlus=true

# Type check
typecheck:
    bun run typecheck

# Run all quality checks
quality: prepare typecheck

# Run all verification checks
ready: quality build
