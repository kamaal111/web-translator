set export
set dotenv-load

CONTAINER_NAME := "web-translator"
PORT := "3000"
AUTH_CONFIG := "src/auth/better-auth.ts"
AUTH_SCHEMA := "src/db/schema/better-auth.ts"

# List available commands
default:
    just --list --unsorted

# Run the Docker container
run: stop delete
    #!/usr/bin/env zsh

    docker run -dp {{ PORT }}:{{ PORT }} -e DATABASE_URL=$DATABASE_URL \
        -e BETTER_AUTH_SECRET=$BETTER_AUTH_SECRET --name {{ CONTAINER_NAME }} {{ CONTAINER_NAME }}

# Stop Docker container
stop:
    docker stop {{ CONTAINER_NAME }} || true

# Delete Docker container
delete:
    docker rm {{ CONTAINER_NAME }} || true

# Delete image
delete-image:
    docker image rm -f {{ CONTAINER_NAME }} || true

# Run dev server
dev: prepare migrate
    #!/usr/bin/env zsh

    export DEBUG="true"

    bun run dev

# Prepare project for development
prepare: install-modules

# Install modules
install-modules:
    bun install

# Generate authentication database tables
make-auth-tables: prepare
    bunx @better-auth/cli generate --config {{ AUTH_CONFIG }} --output {{ AUTH_SCHEMA }} --yes

# Generate migrations
make-migrations: prepare
    bunx drizzle-kit generate

# Run database migrations
migrate:
    bunx drizzle-kit migrate

# Run tests
test:
    bun run test

# Download OpenAPI spec
download-spec output_schema_filepath: prepare
    bun run scripts/download-openapi-spec.ts "{{ output_schema_filepath }}"

# Type check
typecheck:
    bun run typecheck

# Run all quality checks
quality: prepare typecheck

# Run all verification checks
ready: quality test
